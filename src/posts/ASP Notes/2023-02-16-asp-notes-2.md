---
icon: pen-to-square
date: 2023-02-16
tag:
  - Answer Set Programming
category:
  - Answer Set Programming
---

# Aggregates聚合语法

Aggregate就是一些在集合上执行的函数，集合通常是依照某些条件构建出来的。

<!-- more -->

## 规则体中的aggregates

基本形式为：

$$
s_1 \prec_1 \alpha\;\{ \mathbf{t}_1:\mathbf{L}_1;\dots;\mathbf{t}_n:\mathbf{L}_n\} \prec_2 s_2
$$

其中，$\mathbf{t_i}$是原子或者原子构成的元组，$\mathbf{L_i}$是文字或文字的合取。其含义是，由$\mathbf{L_i}$为真的$\mathbf{t_i}$构成的集合上，应用函数$\alpha$，其结果满足$s_1\prec_1$和$\prec_2 s_2$。

由于是集合，所以$\mathbf{t_i}$是不计重复项的：
```prolog
r(X) :- #sum{1:a; 1:b} = X.
a.
b.
```
它只有一个回答集：`a b r(1)`。

值得区分的是，何时要将条件放在Aggregate中，何时要放在外面。下面的程序：
```prolog
a(1).
b(1..2).
x(M, N):- a(M), N=1.
x(M, N):- b(M), N=2.
r(X) :- X = #count{M,N : x(M,N)}, b(N).

#show r/1.
```
回答集是`r(1) r(2)`。它表示当`b(N)`成立时，`X`等于满足`x(M,N)`的元组`(M,N)`的个数。而把`b(N)`移到里面：
```prolog
a(1).
b(1..2).
x(M, N):- a(M), N=1.
x(M, N):- b(M), N=2.
r(X) :- X = #count{M,N : x(M,N), b(N)}.

#show r/1.
```
回答集是`r(3)`。它表示`X`等于满足`x(M,N)`且`b(N)`的元组`(M,N)`的个数，显然有`(1,1)(1,2)(2,2)`三个。

允许使用的函数$\alpha$有：`#count`, `#sum`, `#sum+`, `#min`, `#max`。当$\mathbf{t}_i$是元组时，后面几个函数是针对元组的首项而言的。注意区分：
```prolog
r(X) :- #sum{1:a; 1:b} = X.
a.
b.
```
和
```prolog
r(X) :- #sum{1,1:a; 1,2:b} = X.
a.
b.
```
后者的回答集是`a b r(2)`。

## 规则头中的aggregates

基本形式为：

$$
s_1 \prec_1 \alpha\;\{ \mathbf{t}_1:L_1:\mathbf{L}_1;\dots;\mathbf{t}_n:L_n:\mathbf{L}_n\} \prec_2 s_2
$$

其中的$L_i$是文字，其他与规则体中的Aggregate相同。当$\mathbf{L_i}$为空时，可以省略为$\mathbf{t}_i:L_i$。规则头中的Aggregate与规则体中的区别在于，规则头（无论是Aggregate还是普通的atom），都必须产生一个derivable的结果。看下面的例子：
```prolog
1=#count{1:a;2:b;3:c}.
```
它有三个回答集：`a`，`b`，和`c`。注意这个例子中$\mathbf{L_i}$为空。类似的例子：
```prolog
5=#sum{1:a;2:b;3:c;4:d}.
```
它的回答集是`c b`和`d a`。而
```prolog
5=#sum{1:a;2:b;3:c;4:d:x}.
:- x.
```
的回答集只有`c b`一个了，这个例子展示了$\mathbf{L_i}$的作用。

## 缩略语法

- $\alpha$可以被省略，默认为`#count`。
- $s_1 \prec_1$和$\prec_2 s_2$可以被省略。对于规则体中的aggregates来说同时省略二者没有意义（会使整个aggregate恒真），对于规则头中的来说相当于没有任何限制（即任意选择），此时一般也会省略$\alpha$，因为除了`#count`以外的$\alpha$也没有意义。
- $\prec_1$和$\prec_2$可以被省略（不省略$s_1$和/或$s_2$），此时默认为$\leq$。
- 当$\alpha$是`#count`（或者省略）时，$\mathbf{t}_i$可以被省略。
    - 对于规则头中的aggregates，省略$\mathbf{t}_i$会使gringo在grounding时自动生成形如$x,A_i$的$\mathbf{t}_i$。其中$x$是$L_i$中$\mathrm{not}$的数量，$A_i$是$L_i$中的Atom。这样的组合唯一地编码了一个$L_i$（因为单纯的$L_i$并不能作为$\mathbf{t}_i$使用，其中可能含有否定）
    - 对于规则体中的aggregates，形如$s_1 \prec_1 \{ L_1:\mathbf{L}_1;\dots;L_n:\mathbf{L}_n\} \prec_2 s_2$的aggregate代表$s_1 \prec_1 \mathrm{\#count}\;\{ \mathbf{t}_1:L_1,\mathbf{L}_1;\dots;\mathbf{t}_n:L_n,\mathbf{L}_n\} \prec_2 s_2$，其中的$\mathbf{t}_i$根据$L_i$自动产生，产生规则同规则头中的aggregates。
- 对于规则头中的aggregates，当$\mathbf{L}_i$为空时可以省略$:\mathbf{L}_i$。

根据以上缩略语法，有一种常用的写法为：
```prolog
{a;b}.
```
它利用了上面的第1、2、4、5条缩略语法。根据第1、2条缩略语法，它代表了从集合中选择任意个元素，该规则都可以被满足。根据第4、5条缩略语法，很明显这里的`a`和`b`都是中间项$L_i$，因此该规则将产生`（空）`、`a`、`b`、`a b`四个回答集。这样的语法可以给出集合中任意个项的所有组合。
